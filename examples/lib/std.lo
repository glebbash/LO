// str literal type
struct str {
    data: *&u8,
    size: u32,
}

// constants

const u32::MAX = 4_294_967_295

global STATIC_DATA_SIZE = @data_size()

// abort

type AbortCode = u32

const AbortCode::SHOULD_NOT_HAPPEN = 0
const AbortCode::OOM = 1
const AbortCode::STACK_OVERFLOW = 2
const AbortCode::STACK_UNDERFLOW = 3
const AbortCode::VEC_OUT_OF_BOUNDS = 4
const AbortCode::STACK_VECTOR_GROW = 5

fn abort(abort_code: AbortCode): never {
    // error indicator
    *(0 as &u32) = 69420
    *(4 as &u32) = abort_code
    @unreachable()
}

// memory instructions

const mem::PAGE_SIZE = 65_536

fn mem::align(size: u32): u32 {
    return mem::align_to(size, sizeof &void)
}

fn mem::align_to(size: u32, size_multiple: u32): u32 {
    let remaining_to_align = size % size_multiple
    if remaining_to_align == 0 {
        return size
    }
    let padding = size_multiple - remaining_to_align
    return size + padding
}

macro mem::copy!(src: &void, dest: &void, size: u32) {
    @memory_copy(dest as u32, src as u32, size)
}

fn mem::swap(ref1: &void, ref2: &void, size: u32) {
    while size != 0 {
        size -= 1
        let tmp = *((ref1 as u32 + size) as &u8)
        *((ref1 as u32 + size) as &u8) = *((ref2 as u32 + size) as &u8)
        *((ref2 as u32 + size) as &u8) = tmp
    }
}

// allocator interface

type Alloc = &void
const Alloc::STACK = 0 as Alloc
const Alloc::HEAP = 1 as Alloc

macro new!(a: Alloc, value: infer T): &T {
    let ptr = alloc(a, sizeof T) as &T
    *ptr = value
    ptr
}

macro alloc!<T>(a: Alloc): &T {
    alloc(alloc, sizeof T) as &T
}

macro alloc_many!<T>(a: Alloc, count: u32): *&T {
    alloc(alloc, count * sizeof T) as *&T
}

fn alloc(a: Alloc, size: u32): &void {
    if a == Alloc::STACK {
        return stack::alloc(size)
    }

    if a == Alloc::HEAP {
        return heap::alloc(size)
    }

    return (a as &Arena).alloc(size)
}

macro free!<T>(a: Alloc, ptr: &T) {
    free(a, ptr, sizeof T)
}

macro free_many!<T>(a: Alloc, ptr: *&T, count: u32) {
    free(alloc, ptr, count * sizeof T)
}

fn free(a: Alloc, ptr: &void, size: u32) {
    if a == Alloc::STACK {
        return stack::free(size)
    }

    if a == Alloc::HEAP {
        return heap::free(ptr)
    }

    // arena free is a noop
}

// stack allocator

// 1MB, ~20 pages (65_536b)
global stack::SIZE = 1_048_576

// not initialized yet marker
global stack::MIN = u32::MAX

global stack::NOW = 0

macro stack::alloc!<T>(): &T {
    stack::alloc(sizeof T) as &T
}

fn stack::alloc(size: u32): &void {
    size = mem::align(size)
    stack::check_init!()
    stack::NOW += size
    if stack::NOW > stack::MIN + stack::SIZE {
        abort(AbortCode::STACK_OVERFLOW)
    }
    return (stack::NOW - size) as &void
}

macro stack::check_init!() {
    if stack::MIN == u32::MAX {
        stack::MIN = mem::align(STATIC_DATA_SIZE)
        stack::NOW = stack::MIN
    }
}

macro stack::free!<T>() {
    stack::free(sizeof T)
}

fn stack::free(size: u32) {
    size = mem::align(size)
    stack::NOW -= size
    if stack::NOW < stack::MIN {
        abort(AbortCode::STACK_UNDERFLOW)
    }
}

// tmp alloc utils (stack + defer free)

macro ref!(value: infer T): &T {
    let value_ref = tmp_alloc!<T>()
    *value_ref = value
    value_ref
}

macro tmp_alloc!<T>(): &T {
    stack::alloc(sizeof T) as &T
    defer stack::free(sizeof T)
}

macro tmp_alloc_many!<T>(count: u32): *&T {
    stack::alloc(count * sizeof T) as *&T
    defer stack::free(count * sizeof T)
}

// heap allocator

struct heap::Block {
    size: u32,
    used: bool,
}

macro heap::Block::data!(&self): &void {
    (self as u32 + sizeof heap::Block) as &void
}

macro heap::Block::next!(&self): &heap::Block {
    (self.data!() as u32 + self.size) as &heap::Block
}

global heap::FIRST_BLOCK = null as &heap::Block

global heap::CURRENT_BLOCK = null as &heap::Block

macro heap::new!(value: infer T): &T {
    let ptr = heap::alloc!<T>()
    *ptr = value
    ptr
}

macro heap::alloc!<T>(): &T {
    heap::alloc(sizeof T) as &T
}

macro heap::alloc_many!<T>(count: u32): *&T {
    heap::alloc(count * sizeof T) as *&T
}

// http://dmitrysoshnikov.com/compilers/writing-a-memory-allocator/#mutator-allocator-collector
fn heap::alloc(size: u32): &void {
    size = mem::align(size)
    let free_block = heap::find_free_block(size)
    if free_block != null {
        free_block.used = true
        return free_block.data!()
    }

    if heap::CURRENT_BLOCK == null {
        stack::check_init!()
        heap::CURRENT_BLOCK = (stack::MIN + stack::SIZE) as &heap::Block
        heap::FIRST_BLOCK = heap::CURRENT_BLOCK
    } else {
        heap::CURRENT_BLOCK = heap::CURRENT_BLOCK.next!()
    }

    let new_memory_needed = (
        heap::CURRENT_BLOCK as u32 + sizeof heap::Block + size,
    ) as i32 - @memory_size() * mem::PAGE_SIZE as i32
    if new_memory_needed > +0 {
        let pages_needed = (new_memory_needed as u32 + mem::PAGE_SIZE) \
            / mem::PAGE_SIZE
        if @memory_grow(pages_needed) == -1 {
            abort(AbortCode::OOM)
        }
    }

    heap::CURRENT_BLOCK.size = size
    heap::CURRENT_BLOCK.used = true

    return heap::CURRENT_BLOCK.data!()
}

fn heap::free(ptr: &void) {
    let block = heap::get_block(ptr)
    block.used = false
}

fn heap::get_block(ptr: &void): &heap::Block {
    return (ptr as u32 - sizeof heap::Block) as &heap::Block
}

// first fit
fn heap::find_free_block(size: u32): &heap::Block {
    let block = heap::FIRST_BLOCK
    if block == null {
        return null
    }
    loop {
        if !block.used && block.size >= size {
            return block
        }
        if block as u32 == heap::CURRENT_BLOCK as u32 {
            break
        }
        block = block.next!()
    }
    return null
}

// arena allocator

struct Arena {
    head: &Arena::Block,
    tail: &Arena::Block,
    block_size: u32,
    alloc: Alloc,
}

struct Arena::Block {
    next: &Arena::Block,
    size: u32,
    used: u32,
    base: &void,
}

fn Arena::Block::new(a: Alloc, size: u32): &Arena::Block {
    let mem = alloc(a, size)
    return new!(a, Arena::Block {
        next: null,
        size: size,
        used: 0,
        base: mem,
    })
}

fn Arena::new(block_size: u32): &Arena {
    return Arena::new_in(Alloc::HEAP, block_size)
}

fn Arena::new_in(a: Alloc, block_size: u32): &Arena {
    let block = Arena::Block::new(a, block_size)
    return new!(a, Arena {
        head: block,
        tail: block,
        block_size: block_size,
        alloc: a,
    })
}

macro Arena::new!(&self, value: infer T): &T {
    let ptr = self.alloc(sizeof T) as &T
    *ptr = value
    ptr
}

macro Arena::alloc!<T>(&self): &T {
    self.alloc(sizeof T) as &T
}

macro Arena::alloc_many!<T>(&self, count: u32): *&T {
    self.alloc(count * sizeof T) as *&T
}

fn Arena::alloc(&self, size: u32): &void {
    size = mem::align(size)
    let block = self.tail

    if block.used + size > block.size {
        let new_block_size = self.block_size
        if size > self.block_size {
            new_block_size = size
        }
        let new_block = Arena::Block::new(self.alloc, new_block_size)
        self.tail.next = new_block
        self.tail = new_block
        block = new_block
    }

    let ptr = (block.base as u32 + block.used) as &void
    block.used += size
    return ptr
}

fn Arena::reset(&self) {
    let block = self.head
    loop {
        block.used = 0
        if block.next == null {
            break
        }
        block = block.next
    }
    self.tail = self.head
}

fn Arena::free(&self) {
    let block = self.head
    loop {
        free(self.alloc, block.base, block.size)
        let next_block = block.next
        free!<Arena::Block>(self.alloc, block)
        if next_block == null {
            break
        }
        block = next_block
    }
    free!<Arena>(self.alloc, self)
}

// vec
struct Vec {
    data: &void,
    size: u32,
    capacity: u32,
    item_size: u32,
    alloc: Alloc,
}

macro Vec::new!<T>(): &Vec {
    Vec::new(sizeof T)
}

fn Vec::new(item_size: u32): &Vec {
    return Vec::with_capacity(10, item_size)
}

fn Vec::new_in(a: Alloc, item_size: u32): &Vec {
    return Vec::with_capacity_in(a, 10, item_size)
}

macro Vec::with_capacity!<T>(capacity: u32): &Vec {
    Vec::with_capacity(capacity, sizeof T)
}

fn Vec::with_capacity(capacity: u32, item_size: u32): &Vec {
    return Vec::with_capacity_in(Alloc::HEAP, capacity, item_size)
}

fn Vec::with_capacity_in(a: Alloc, capacity: u32, item_size: u32): &Vec {
    return new!(a, Vec {
        data: alloc(a, capacity * item_size),
        size: 0,
        capacity: capacity,
        item_size: item_size,
        alloc: a,
    })
}

macro Vec::free_deep!<T>(&self) {
    for i in 0..self.size {
        self.get!<T>(i).free()
    }
}

fn Vec::free(&self) {
    free!<Vec>(self.alloc, self)
    free(self.alloc, self.data, self.capacity)
}

macro Vec::at!<T>(&self, index: u32): &T {
    self.at(index) as &T
}

macro Vec::get!<T>(&self, index: u32): T {
    *self.at!<T>(index)
}

macro Vec::set!(&self, index: u32, item: infer T) {
    *self.at!<T>(index) = item
}

fn Vec::at(&self, index: u32): &void {
    self._panic_if_out_of_bounds!(index)
    return (self.data as u32 + index * self.item_size) as &void
}

fn Vec::at_unchecked(&self, index: u32): &void {
    return (self.data as u32 + index * self.item_size) as &void
}

fn Vec::swap(&self, index1: u32, index2: u32) {
    self._panic_if_out_of_bounds!(index1)
    self._panic_if_out_of_bounds!(index2)
    let item_size = self.item_size
    let item_addr1 = (self.data as u32 + index1 * item_size) as &void
    let item_addr2 = (self.data as u32 + index2 * item_size) as &void
    mem::swap(item_addr1, item_addr2, item_size)
}

macro Vec::push!(&self, item: infer T) {
    *(self.push() as &T) = item
}

fn Vec::push(&self): &void {
    self._grow_to_size(self.size + 1)
    return self.at_unchecked(self.size - 1)
}

fn Vec::push_all(&self, items: &void, items_len: u32) {
    let prev_len = self.size
    self._grow_to_size(prev_len + items_len)
    let items_dest = (self.data as u32 + prev_len * self.item_size) as &void
    mem::copy!(items, items_dest, items_len * self.item_size)
}

fn Vec::append(&self, other: &Vec) {
    self.push_all(other.data, other.size)
    other.size = 0
}

fn Vec::pop_unchecked(&self): &void {
    self.size -= 1
    return self.at_unchecked(self.size)
}

fn Vec::reverse(&self) {
    self.reverse_range(0, self.size - 1)
}

fn Vec::reverse_range(&self, start: u32, end: u32) {
    while start < end {
        self.swap(start, end)
        start += 1
        end -= 1
    }
}

fn Vec::_grow_to_size(&self, new_size: u32) {
    self.size = new_size
    if self.size <= self.capacity {
        return
    }
    if self.alloc == Alloc::STACK {
        abort(AbortCode::STACK_VECTOR_GROW)
    }
    let item_size = self.item_size
    let old_capacity = self.capacity
    let old_data = self.data
    defer free(self.alloc, old_data, old_capacity * item_size)
    self.capacity = old_capacity * 2
    self.data = alloc(self.alloc, self.capacity * item_size)
    mem::copy!(old_data, self.data, self.size * item_size)
}

macro Vec::_panic_if_out_of_bounds!(&self, index: u32) {
    if index < 0 || index >= self.size {
        abort(AbortCode::VEC_OUT_OF_BOUNDS)
    }
}

// string
struct String {
    chars: &Vec of u8,
}

fn String::new(capacity: u32): String {
    return String {
        chars: Vec::with_capacity!<u8>(capacity),
    }
}

fn String::new_in(a: Alloc, capacity: u32): String {
    return String {
        chars: Vec::with_capacity_in(a, capacity, sizeof u8),
    }
}

fn String::from_chars(chars: &Vec of u8): String {
    return String { chars: chars }
}

fn String::from_str(value: str): String {
    return String::from_str_in(Alloc::HEAP, value)
}

fn String::from_str_in(a: Alloc, value: str): String {
    let string = String::new(value.size)
    string.chars.push_all(value.data, value.size)
    return string
}

fn String::free(self) {
    self.chars.free()
}

fn String::clone(self): String {
    return String::from_str(self.as_str())
}

fn String::len(self): u32 {
    return self.chars.size
}

fn String::push_str(self, value: str) {
    self.chars.push_all(value.data, value.size)
}

fn String::push(self, c: u8) {
    self.chars.push!(c)
}

fn String::char_at(self, index: u32): u8 {
    return *self.chars.at!<u8>(index)
}

// in-place modification, returns self
fn String::replace(self, search: str, replacement: str): String {
    if search.size == 0 {
        return self
    }

    if self.chars.alloc == Alloc::STACK {
        abort(AbortCode::STACK_VECTOR_GROW)
    }

    let original = self.as_str()
    let replaced = String::new_in(self.chars.alloc, self.len())

    let pos = 0
    loop {
        let index = original.find(search, pos) catch _ {
            replaced.push_str(original.slice(pos, original.size))
            break
        }

        replaced.push_str(original.slice(pos, index))
        replaced.push_str(replacement)

        pos = index + search.size
    }

    self.chars.free()
    self.chars = replaced

    return self
}

fn String::join(items: &Vec of str, separator: u8): String {
    let output = String::new(32)
    for i in 0..items.size {
        output.push_str(items.get!<str>(i))
        if i + 1 < items.size {
            output.push(separator)
        }
    }
    return output
}

fn String::as_str(self): str {
    return str {
        data: self.chars.data as *&u8,
        size: self.chars.size,
    }
}

// str utils

fn str::char_at(self, index: u32): u8 {
    return *array_at!<u8>(self.data, index)
}

fn str::clone(self): str {
    let new_data = heap::alloc_many!<u8>(self.size)
    mem::copy!(self.data, new_data, self.size)

    return str { data: new_data, size: self.size }
}

fn str::equals(self, other: str): bool {
    if self.size != other.size {
        return false
    }

    for i in 0..self.size {
        if self.char_at(i) != other.char_at(i) {
            return false
        }
    }

    return true
}

fn str::find(self, search: str, start: u32): Result<u32, &void> {
    if search.size == 0 {
        return Ok(start)
    }

    if search.size > self.size {
        return Err(1 as &void)
    }

    for i in start..(self.size - search.size + 1) {
        if self.slice(i, i + search.size).equals(search) {
            return Ok(i)
        }
    }

    return Err(1 as &void)
}

fn str::starts_with(self, other: str): bool {
    return self.slice(0, other.size).equals(other)
}

fn str::skip_chars(self, char_count: u32): str {
    return self.slice(char_count, self.size)
}

fn str::slice(self, start: u32, end: u32): str {
    if start >= self.size || end > self.size || start > end {
        return ""
    }

    return str {
        data: (self.data as u32 + start) as *&u8,
        size: end - start,
    }
}

fn str::from_cstr(value: *&u8): str {
    let len = 0
    while *array_at!<u8>(value, len) != '\0' {
        len += 1
    }
    return str { data: value, size: len }
}

fn str::split(self, separator: u8, output: &Vec of str): &Vec of str {
    let start = 0

    for i in 0..self.size {
        if self.char_at(i) == separator {
            output.push!(self.slice(start, i))
            start = i + 1
        }
    }

    output.push!(self.slice(start, self.size))

    return output
}

// conversions
macro u32::to_tmp_str!(self): str {
    let s = String::new_in(Alloc::STACK, 10)
    defer s.free()

    self.write_to(s)
    s.as_str()
}

macro u32::to_str_in!(self, a: Alloc): str {
    let s = String::new_in(a, 10)
    self.write_to(s)
    s.as_str()
}

fn u32::write_to(self, output: String) {
    let start_size = output.len()
    if self == 0 {
        output.push('0')
        return
    }
    loop {
        let rem = self % 10
        output.push('0' + rem as u8)
        self = self / 10
        if self == 0 {
            break
        }
    }
    output.chars.reverse_range(start_size, output.len() - 1)
}

macro u64::to_tmp_str!(self): str {
    let s = String::new_in(Alloc::STACK, 20)
    defer s.free()

    self.write_to(s)
    s.as_str()
}

fn u64::write_to(self, output: String) {
    let start_size = output.len()
    if self == 0u64 {
        output.push('0')
        return
    }
    loop {
        let rem = self % 10u64
        output.push('0' + rem as u32 as u8)
        self = self / 10u64
        if self == 0u64 {
            break
        }
    }
    output.chars.reverse_range(start_size, output.len() - 1)
}

// utils

macro todo!(): never {
    @unreachable()
}

macro array_at!<T>(array: *&T, index: u32): &T {
    (array as u32 + index * sizeof T) as &T
}
