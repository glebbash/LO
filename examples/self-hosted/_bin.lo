include "../lib/args.lo"
include "../lib/wasi.lo"

include "./core.lo"
include "./lexer.lo"
include "./compiler.lo"

export memory {
    min_pages: 1,
}

const USAGE = "Usage:
  lo lex <input.lo> (debug)"

export fn _start() {
    let args = Args::load()
    defer args.free()

    if args.argc < 3 {
        stderr_writeln(USAGE)
        wasi::proc_exit(1)
    }

    let file_name = args.get(2)

    if args.get(1).equals("lex") {
        let compiler = Compiler::new()
        defer compiler.free()

        let module = compiler.include(
            file_name,
            ref!(Loc::internal()),
        ) catch _ {
            wasi::proc_exit(1)
        }

        let a = Arena::new()
        defer a.free()

        let lexer = module.parser.lexer

        let file_path = compiler.fm.get(lexer.file_index).absolute_path
        do stdout_write(it) with (
            "file_path: ", \\ file_path.as_str(), \\ "\n",
        )

        for i in 0..lexer.tokens.size {
            let token = lexer.tokens.get!<Token>(i)

            defer a.reset()

            do stdout_write(it) with (
                token.loc.pos.line.to_tmp_str!(),
                ":", \\ token.loc.pos.col.to_tmp_str!(),
                "-",
                token.loc.end_pos.line.to_tmp_str!(),
                ":", \\ token.loc.end_pos.col.to_tmp_str!(),

                " ", \\ "(", \\ token.loc.pos.offset.to_tmp_str!(),
                "-", \\ token.loc.end_pos.offset.to_tmp_str!(), \\ ")",

                " ", \\ token.type_.to_str(),

                " ", \\ ">> ",
                token.loc.read_span(lexer.source) \
                    |> String::from_str_in(a, it).replace("\n", "\\n") \
                    |> it.as_str(),
                " <<",

                "\n",
            )
        }

        return
    }

    do stderr_write(it) with (
        "Unknown command: ", \\ args.get(1), \\ "\n",
        USAGE, \\ "\n",
    )
    wasi::proc_exit(1)
}
