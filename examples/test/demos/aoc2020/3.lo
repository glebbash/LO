include "../../../lib/cli.lo"
include "../../../lib/fs.lo"

type Tile = bool

const Tile::TREE = true
const Tile::EMPTY = false
const SLOPE_I = 1
const SLOPE_J = 3

fn main() {
    let input = fs::read_file!("./examples/test/demos/aoc2020/3.txt")
    defer input.free()
    let map = Map::parse(input)
    defer map.free()
    let tree_count = 0
    let i = 0
    let j = 0
    loop {
        i += SLOPE_I
        j += SLOPE_J
        if i == map.rows.size {
            break
        }
        if map.get(i, j) == Tile::TREE {
            tree_count += 1
        }
    }
    print_u32(tree_count)
    puts("\n")
}

struct Map {
    rows: &Vec of &Vec of Tile,
}

fn Map::parse(input: String): Map {
    let rows = Vec::new!<&Vec of Tile>()
    let current_row = Vec::new!<Tile>()
    for i in 0..input.len() {
        let c = input.char_at(i)
        if c == '#' {
            current_row.push!(Tile::TREE)
        }
        if c == '.' {
            current_row.push!(Tile::EMPTY)
        }
        if c == '\n' {
            rows.push!(current_row)
            current_row = Vec::new!<Tile>()
        }
    }
    return .Map { rows: rows }
}

fn Map::get(self, i: u32, j: u32): Tile {
    let row = self.rows.get!<&Vec of Tile>(i)
    return *(row.at(j % row.size) as &Tile)
}

fn Map::free(self) {
    for i in 0..self.rows.size {
        let row = self.rows.get!<&Vec of Tile>(i)
        row.free()
    }
    self.rows.free()
}
