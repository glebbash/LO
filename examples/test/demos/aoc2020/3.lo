@include("../../../lib/cli.lo")
@include("../../../lib/fs.lo")

type Tile = bool

inline let Tile::TREE = true
inline let Tile::EMPTY = false
inline let SLOPE_I = 1
inline let SLOPE_J = 3

fn main() {
    let input = fs::read_file!("./examples/test/demos/aoc2020/3.txt")
    defer input.free()

    let map = Map::parse(input)
    defer map.free()

    let tree_count = 0
    let i = SLOPE_I
    let j = SLOPE_J
    while i < map.rows.len {
        if map.get(i, j) == Tile::TREE {
            tree_count += 1
        }

        i += SLOPE_I
        j += SLOPE_J
    }

    println(tree_count.to_tmp_str!())
}

type Map = struct {
    rows: &Vec(&Vec(Tile)),
}

fn Map::parse(input: String): Map {
    let rows = Vec::new!<&Vec(Tile)>()
    let current_row = Vec::new!<Tile>()
    for i in 0..input.len() {
        let c = input.char_at(i)
        if c == '#' {
            current_row.push!(Tile::TREE)
        }
        if c == '.' {
            current_row.push!(Tile::EMPTY)
        }
        if c == '\n' {
            rows.push!(current_row)
            current_row = Vec::new!<Tile>()
        }
    }

    return Map { rows: rows }
}

fn Map::get(self, i: u32, j: u32): Tile {
    let row = self.rows.get!(i)
    return *(row.at(j % row.len) as &Tile)
}

fn Map::free(self) {
    for i in 0..self.rows.len {
        let row = self.rows.get!(i)
        row.free()
    }

    self.rows.free()
}
